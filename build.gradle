/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.gradle.plugin-publish' version '0.9.7'
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = 'io.dreampie.gradle'
archivesBaseName = 'thrift-gradle-plugin'
version = '0.5.0'
description = 'A gradle plugin for compiling Thrift IDL files using thrift compiler'

ext {
    title = 'Gradle Thrift Plugin'
    organizationId = 'io.dreampie'
    organizationName = 'Dreampie'
    organizationUrl = 'http://www.dreampie.io/'

    websiteUrl = 'https://github.com/Dreampie/thrift-gradle-plugin'
    issueTrackerUrl = 'https://github.com/Dreampie/thrift-gradle-plugin/issues'
    vcsUrl = 'https://github.com/Dreampie/thrift-gradle-plugin.git'

    licenseName = 'Apache License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

    createdBy = "${System.getProperty('java.version')} (${System.getProperty('java.vm.vendor')})"
}

configurations.all {
    transitive = false
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

processResources {

    from(['LICENSE', 'NOTICE']) {
        into 'META-INF'
    }
}

compileGroovy {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes(
                'Specification-Title': title,
                'Specification-Version': version,
                'Specification-Vendor': organizationName,
                'Implementation-Title': title,
                'Implementation-Version': version,
                'Implementation-Vendor': organizationName,
                'Implementation-Vendor-Id': organizationId,
                'Implementation-Vendor-URL': organizationUrl,
                'Created-By': createdBy,
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId archivesBaseName

            from components.java

            pom.withXml {
                def node = asNode()
                node.children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name title
                    description description
                    url websiteUrl
                    inceptionYear '2014'
                    scm {
                        url websiteUrl
                        connection "scm:$vcsUrl"
                        developerConnection "scm:$vcsUrl"
                    }
                    licenses {
                        license {
                            name licenseName
                            url licenseUrl
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'Dreampie'
                            name 'Dreampie'
                            email 'Dreampie@outlook.com'
                        }
                    }
                }
            }
        }
    }
}

pluginBundle {
    website = "$websiteUrl"
    vcsUrl = "$vcsUrl"
    description = "${project.description}"
    tags = ['thrift']

    plugins {
        thriftPlugin {
            id = 'io.dreampie.thrift'
            displayName = title
        }
    }
}

