/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'com.jfrog.bintray' version '1.2'
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = 'org.jruyi.gradle'
archivesBaseName = 'thrift-gradle-plugin'
version = '0.2.1-SNAPSHOT'
description = 'A gradle plugin for compiling Thrift IDL files using thrift compiler'

ext {
	title = 'Thrift Gradle Plugin'
	organizationId = 'org.jruyi'
	organizationName = 'JRuyi.org'
	organizationUrl = 'http://www.jruyi.org/'

	licenseName = 'Apache License, Version 2.0'
	licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

	createdBy = "${System.getProperty('java.version')} (${System.getProperty('java.vm.vendor')})"
}

configurations.all {
	transitive = false
}

dependencies {
	compile gradleApi()
	compile localGroovy()
}

processResources {

	from(['LICENSE', 'NOTICE']) {
		into 'META-INF'
	}
}

compileGroovy {
	sourceCompatibility = 1.6
	targetCompatibility = 1.6
	options.encoding = 'UTF-8'
}

jar {
	manifest {
		attributes(
				'Specification-Title': title,
				'Specification-Version': version,
				'Specification-Vendor': organizationName,
				'Implementation-Title': title,
				'Implementation-Version': version,
				'Implementation-Vendor': organizationName,
				'Implementation-Vendor-Id': organizationId,
				'Implementation-Vendor-URL': organizationUrl,
				'Created-By': createdBy,
		)
	}
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			artifactId archivesBaseName

			from components.java

			pom.withXml {
				def projectNode = asNode()
				projectNode.appendNode('name', title)
				projectNode.appendNode('description', description)
				projectNode.appendNode('url', organizationUrl)

				def organizationNode = projectNode.appendNode('organization')
				organizationNode.appendNode('name', organizationName)
				organizationNode.appendNode('url', organizationUrl)

				def licenseNode = projectNode.appendNode('licenses').appendNode('license')
				licenseNode.appendNode('name', licenseName)
				licenseNode.appendNode('url', licenseUrl)
				licenseNode.appendNode('distribution', 'repo')
			}

			artifact sourcesJar
		}
	}
}

bintray {
	user = bintrayUser
	key = bintrayApiKey
	publications = ['mavenJava']
	publish = true
	pkg {
		repo = 'maven'
		userOrg = 'jruyi'
		name = archivesBaseName
		desc = description
		websiteUrl = 'https://github.com/jruyi/thrift-gradle-plugin'
		issueTrackerUrl = 'https://github.com/jruyi/thrift-gradle-plugin/issues'
		vcsUrl = 'https://github.com/jruyi/thrift-gradle-plugin.git'
		licenses = ['Apache-2.0']
		labels = ['thrift', 'gradle', 'plugin']
		publicDownloadNumbers = true
		version {
			name = project.version
			vcsTag = "v${project.version}"
			attributes = ['gradle-plugin': 'org.jruyi.thrift:org.jruyi.gradle:thrift-gradle-plugin']
		}
	}
}
